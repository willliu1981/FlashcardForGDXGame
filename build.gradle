buildscript {


    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.2'


    }
}

allprojects {
    apply plugin: "eclipse"

    version = '1.0'
    ext {
        appName = "FlashcardForLibgdxGame"
        gdxVersion = '1.12.0'
        roboVMVersion = '2.3.20'
        box2DLightsVersion = '1.5'
        ashleyVersion = '1.7.4'
        aiVersion = '1.8.2'
        gdxControllersVersion = '2.2.1'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://jitpack.io" }
    }
}

project(":desktop") {
    apply plugin: "java-library"

    // 定義版本資訊
    ext {
        appName = "MyLibGDXApp"
        versionCode = 28
        versionName = "1.0"
    }


    dependencies {
        implementation project(":core")
        api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        api "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
        implementation 'org.xerial:sqlite-jdbc:3.34.0'

        implementation project(':common')

    }


    task generateVersionClass {
        doLast {
            def className = 'DesktopVersionHelper'
            def packageName = 'idv.kuan.flashcard.gdx.game'
            def srcDir = projectDir.toString() + '/src/' + packageName.replace('.', '/')
            def file = new File(srcDir, "${className}.java")
            println "Generating Version class at: ${file.absolutePath}" // 添加打印語句

            if (!file.parentFile.exists()) {
                file.parentFile.mkdirs()
            }

            file.text = """
        package ${packageName};
        import idv.kuan.libs.impl.DefaultVersionHelper;

        public class ${className} extends DefaultVersionHelper{
                public String getVersionName() {
                    return "${project.ext.versionName}";
                }

                public int getVersionCode() {
                    return ${project.ext.versionCode};
                }
        }
        """
        }
    }


    compileJava.dependsOn generateVersionClass
}

project(":android") {
    apply plugin: "com.android.application"


    configurations { natives }

    ext {
        // 定義全局變量
        versionCodeGlobal = 11
        versionNameGlobal = "1.0"
    }

    dependencies {
        implementation project(":core")
        api "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
        implementation 'org.sqldroid:sqldroid:1.0.3'

        implementation project(':kuanandroidlibs')
        implementation project(':common')
    }
}

project(":core") {
    apply plugin: "java-library"


    dependencies {
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"

        implementation project(':common')
        implementation project('test:lib')

    }
}
